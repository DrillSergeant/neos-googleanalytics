prototype(Neos.GoogleAnalytics:TrackingCode) < prototype(Neos.Fusion:Component) {
    # Don't track while in backend
    @if.inLiveWorkspace = ${node.context.workspaceName == 'live'}

    # Include tracking code if a tracking id is configured for the current site
    @if.trackingIdIsSet = ${!String.isBlank(this.trackingId)}

    # Do not show if enableTracking is false
    @if.trackingIsEnabled = ${Configuration.setting('Neos.GoogleAnalytics.enableTracking')}

    # Tracking ID is blank by default:
    trackingId = ''

    renderer = Neos.Fusion:Component {
        # Actual tracking code JavaScript snippets, can be extended with additional lines
        code = Neos.Fusion:Join {
            create = 'window.dataLayer = window.dataLayer || [];'
            push = 'function gtag(){dataLayer.push(arguments);}'
            date = 'gtag("js", new Date());'
            config = ${'gtag("config", "' + props.trackingId + '");'}
        }
        trackingId = ${props.trackingId}

        renderer = afx`
            <script async src={"https://www.googletagmanager.com/gtag/js?id=" + props.trackingId}></script>
            <script>{props.code}</script>
        `
    }
}
